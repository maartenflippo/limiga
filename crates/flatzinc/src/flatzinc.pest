model_item = { parameter_declaration | variable_declaration | constraint_declaration | goal }

parameter_declaration = { parameter_type ~ ":" ~ identifier ~ "=" ~ parameter_expression ~ ";" }

parameter_type = {
    basic_parameter_type | 
    "array" ~ "[" ~ index_set ~ "]" ~ "of" ~ basic_parameter_type 
}

parameter_expression = {
    basic_literal_expression |
    "[" ~ basic_literal_expression ~ ("," ~ basic_literal_expression)* ~ "]"
}

basic_parameter_type = { "int" | "bool" }

variable_declaration = { 
    (variable_type ~ ":" ~ identifier ~ ";") |
    "array" ~ "[" ~ index_set ~ "]" ~ "of" ~ variable_type ~ ":" ~ identifier ~ (annotations)? ~ "=" ~ array_literal ~ ";"
}
variable_type = {
    ("var" ~ basic_parameter_type) |
    ("var" ~ int_literal ~ ".." ~ int_literal)
}

constraint_declaration = { 
    "constraint" ~ identifier ~ "(" ~ expression ~ ("," ~ expression)* ~ ")" ~ ";"
}

array_literal = { "[" ~ basic_expression ~ ("," ~ basic_expression)* ~ "]" }
basic_expression = { basic_literal_expression | identifier }
expression = { basic_expression | array_literal }

goal = { "solve" ~ "satisfy" ~ ";" }

annotations = { 
    "::" ~ identifier ~ ("(" ~ annotation_expression ~ ")")? 
}

annotation_expression = {
    ("[" ~ basic_literal_expression ~ ("," ~ basic_literal_expression)* ~ "]") |
    basic_literal_expression
}

basic_literal_expression = { set_literal | bool_literal | int_literal }

int_literal = @{ 
    ("-"? ~ ('0'..'9')+) |
    ("-"? ~ "0x" ~ ('0'..'9' | 'A'..'F' | 'a'..'f')+) |
    ("-"? ~ "0o" ~ ('0'..'7')+)
}

bool_literal = { "true" | "false" }

set_literal = { int_literal ~ ".." ~ int_literal }

index_set = { "1" ~ ".." ~ int_literal }

identifier = @{ ('A'..'Z' | 'a'..'z' | "_") ~ ('A'..'Z' | 'a'..'z' | '0'..'9' | "_")* }

WHITESPACE = _{ " " }

